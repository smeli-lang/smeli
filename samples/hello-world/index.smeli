@plugin("@smeli/plugin-ui")
@plugin("@smeli/plugin-ace")

# Hello, world!
##> function plot

f: alpha => sin(alpha)

@inline("formula_card")

tex_formula: make_formula_card(katex.transpile(&f))

magic_slider: ui.slider {
  min: -1
  max: 1
  step: 0.01
  value: f(time)
}

offset_slider: ui.slider {
  min: -4
  max: 4
  step: 0.1
}

thing: ui.layout {
  direction: "column"
  surface: "secondary"
  elevation: 2
  item0: tex_formula
  item1: magic_slider
}

plot_card: ui.layout {
  direction: "column"
  surface: "primary"
  elevation: 2
  item0: thing
  item1: ui.layout {
    direction: "column"
    surface: "background"
    elevation: 2
    item0: ui.plot {
      function: x => f(x + offset_slider.value + time)
      point: vec2(0 - offset_slider.value * 1, magic_slider.value)
    }
    item1: offset_slider
  }
}

ui: ui {
  page: layout {
    direction: "responsive_row"
    item0: outline
    item1: plot_card
  }
}

##> another function
f: x => sin(x) + sin(x * 5) * 0.3

##> and yet another one
f: x => sin(x + sin(x * 5) * 0.2) * sin(time * 0.3)

##> fractions
f: x => sin(time * 0.7) / sin(time + x)

#> sliders
thing: thing {
  item0: ui.slider {
    min: 5
    value: 100
  }
  item1: item0 {}
}

f: x => sin(x * 100 / thing.item1.value)

#> code editor

editor: ace.editor {
  code: "x = 42"
}

ui: ui {
  page: layout {
    direction: "responsive_row"
    item0: editor
    item1: ui.layout {
      surface: "secondary"
      elevation: 2
      item0: make_formula_card(editor.code)
    }
  }
}
