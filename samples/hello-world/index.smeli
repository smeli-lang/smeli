@plugin("@smeli/plugin-ui")
@plugin("@smeli/plugin-plot")
@plugin("@smeli/plugin-ace")

ui: ui {
  theme: default_themes.dark
}

# Hello, world!
##> function plot

f: alpha => sin(alpha)

@inline("formula_card")

tex_formula: make_formula_card(katex.transpile(&f))

magic_slider: ui.slider {
  min: -1
  max: 1
  step: 0.01
  value: f(time)
  label: str(value)
}

offset_slider: ui.slider {
  min: -4
  max: 4
  step: 0.1
  label: "x-offset"
}

thing: ui.surface {
  direction: "column"
  color: "primary"
  elevation: 2
  item0: tex_formula
  item1: ui.surface {
    flex: 0
    item0: magic_slider
  }
}

plot_card: ui.surface {
  direction: "column"
  color: "secondary"
  elevation: 2
  item0: thing
  item1: ui.surface {
    direction: "column"
    color: "background"
    elevation: 2
    item0: plot.view {
      viewport: {
        center: vec2(offset_slider.value, 0)
        size: vec2(9, 5)
      }
      item0: plot.grid {}
      item1: plot.point {
        position: vec2(0, magic_slider.value)
        guides: true
        label: "f(x + time)"
      }
      item2: plot.function {
        function: x => f(x + time)
      }
      item3: plot.function {
        function: sin
        color: vec3(0.4, 1, 0.8)
      }
    }
    item1: ui.surface {
      flex: 0
      item0: offset_slider
    }
  }
}

ui: ui {
  page: surface {
    direction: "row"
    responsive: true
    fade: true
    item0: outline
    item1: plot_card
  }
}

##> another function
f: x => sin(x) + sin(x * 5) * 0.3

##> and yet another one
f: x => sin(x + sin(x * 5) * 0.2) * sin(time * 0.3)

##> fractions
f: x => sin(time * 0.7) / sin(time + x)

#> sliders
thing: thing {
  item0: ui.slider {
    min: 5
    value: animate(50 + sin(time * 10) * 20, 50, 4)
    label: "Top Slider"
  }
  item1: item0 {
    label: "Bottom Slider"
  }
}

f: x => sin(x * 100 / thing.item1.value)

#> code editor

editor: ace.editor {
  code: "x = 42"
}

ui: ui {
  page: surface {
    direction: "column"
    responsive: true
    fade: true
    item0: editor
    item1: ui.surface {
      color: "secondary"
      item0: make_formula_card(editor.code)
    }
  }
}
